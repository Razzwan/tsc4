{-
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref.
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

() recv_internal() {
}

const MAX_BITS = 1023;

(int) bits_from_int(int t) inline {
    if (t == 0) {
        return 1;
    }
    int i = 0;
    int r = t;
    while (r > 0) {
        r = r >> 1;
        i += 1;
    }
    return i;
}

(int, int) write_mask(int b, int mask) inline {
    int out = 0;

    if (b & mask) {
        out = 1;
        b = b & (~ mask);
    }

    mask = mask >> 1;

    int l = 1;
    while (mask & ((b & mask) == 0)) {
        out = out << 1;
        mask = mask >> 1;
        l += 1;
    }
    return (out, l);
}

(cell) find_and_replace_slice(int flag, int f_l, int value, int v_l, int buf, int b_l, slice from, builder to) {
    int to_bits = MAX_BITS - to.builder_bits();
    int buf_mask = 1 << (f_l - 1);

    while (true) {
        int need_bits = f_l - b_l;
        while (need_bits) {
            while (from.slice_data_empty?()) {
                if (from.slice_refs_empty?()) {
                    if (to_bits >= b_l) {
                        to~store_uint(buf, b_l);
                        return to.end_cell();
                    } else {
                        to = store_ref(to, begin_cell().store_uint(buf, b_l).end_cell());
                        return to.end_cell();
                    }
                }
                from = from~load_ref().begin_parse();
            }

            int n = min(from.slice_bits(), need_bits);
            buf = buf << n;
            buf = buf | from~load_uint(n);
            need_bits -= n;
            b_l += n;
        }

        int to_buf = 0;
        int to_buf_len = 0;
        if (flag == buf) {
            to_buf = value;
            to_buf_len = v_l;
            b_l = 0;
            buf = 0;
        } else {
            (to_buf, to_buf_len) = write_mask(buf, buf_mask);
            buf = buf & (~ buf_mask);
            b_l -= to_buf_len;
        }

        if (to_bits >= to_buf_len) {
            to~store_uint(to_buf, to_buf_len);
            to_bits -= to_buf_len;
        } else {
            builder to_next = begin_cell();
            if (to_buf_len) {
                to_next~store_uint(to_buf, to_buf_len);
            }
            cell complete_next = find_and_replace_slice(flag, f_l, value, v_l, buf, b_l, from, to_next);
            to = store_ref(to, complete_next);
            return to.end_cell();
        }
    }

    return begin_cell().end_cell();
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
    int f_l = bits_from_int(flag);
    int v_l = bits_from_int(value);

    return find_and_replace_slice(
            flag,
            f_l,
            value,
            v_l,
            0,
            0,
            linked_list.begin_parse(),
            begin_cell()
    );
}
