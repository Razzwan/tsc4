{-
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref.
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

() recv_internal() {
}

const MAX_BITS = 1023;
const NUMBER_BITS = 128;
const BITS_MULTIPLIER = 6;

(int) bits_from_uint(int t) inline {
    if (t == 0) {
        return 1;
    }
    int i = 0;
    int r = t;
    while (r > 0) {
        r = r >> 1;
        i += 1;
    }
    return i;
}

(builder) find_and_replace_slice(slice buf, slice from, builder to, int f, int f_l, int v, int v_l) {
    from = begin_cell()
            .store_ref(
                    begin_cell().store_slice(from).end_cell())
            .end_cell()
            .begin_parse();

    int bits_left = MAX_BITS - to.builder_bits();

    while (~ from.slice_refs_empty?()) | (~ from.slice_data_empty?()) | (buf.slice_bits() >= f_l) {

        if (buf.slice_bits() < f_l) {
            if (from.slice_bits() >= NUMBER_BITS * BITS_MULTIPLIER) {
                builder for_buf = begin_cell()
                        .store_slice(buf);
                repeat (BITS_MULTIPLIER) {
                    for_buf~store_uint(from~load_uint(NUMBER_BITS), NUMBER_BITS);
                }
                buf = for_buf.end_cell().begin_parse();
            } else {
                int buf_l = buf.slice_bits();
                int buf_v = buf~load_uint(buf_l);

                int from_l = from.slice_bits();

                buf = begin_cell()
                        .store_uint(buf_v, buf_l)
                        .store_uint(from~load_uint(from_l), from_l)
                        .end_cell()
                        .begin_parse();

                if (~ from.slice_refs_empty?()) {
                    from = from~load_ref().begin_parse();
                }
            }
        } else {
            int to_load = 0;
            int to_load_l = 0;
            if (f ^ buf.preload_uint(f_l)) {
                to_load = buf~load_uint(1);
                to_load_l = 1;
            } else {
                buf~skip_bits(f_l);
                to_load = v;
                to_load_l = v_l;
            }

            if (bits_left > to_load_l) {
                bits_left -= to_load_l;
                to~store_uint(to_load, to_load_l);
            } else {
                return to.store_ref(find_and_replace_slice(
                        buf,
                        from,
                        begin_cell()
                            .store_uint(to_load, to_load_l),
                        f, f_l, v, v_l
                ).end_cell());
            }
        }
    }

    int to_load_l = buf.slice_bits();
    int to_load = buf~load_uint(to_load_l);

    if (bits_left > to_load_l) {
        return to.store_uint(to_load, to_load_l);
    } else {
        return to.store_ref(
                begin_cell()
                        .store_uint(to_load, to_load_l).end_cell()
        );
    }
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
    int f_l = bits_from_uint(flag);
    int v_l = bits_from_uint(value);

    return find_and_replace_slice(
            begin_cell().end_cell().begin_parse(),
        linked_list.begin_parse(),
        begin_cell(),
        flag,
        f_l,
        value,
        v_l
    ).end_cell();
}
